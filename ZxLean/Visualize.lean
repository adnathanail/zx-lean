import ZxLean.ZXDiagram
import ProofWidgets.Component.HtmlDisplay

open Lean Server ProofWidgets

-- == Python daemon config ==
def daemonHost := "127.0.0.1"
def daemonPort := 5050
def daemonUrl := s!"http://{daemonHost}:{daemonPort}"

-- == Auto-start Python daemon ==
initialize do
  discard <| IO.Process.output {
    cmd := "sh"
    -- Kill any existing pyzx daemon
    args := #["-c", s!"lsof -ti:{daemonPort} | xargs kill 2>/dev/null || true"]
  }
  discard <| IO.Process.spawn {
    cmd := "sh"
    args := #["-c", s!"cd pyzx_daemon && exec uv run python -u app.py --host {daemonHost} --port {daemonPort} --debug >pyzx_daemon.log 2>&1"]
  }

-- == ZXDiagram JSON serialization (`ZXDiagram` to `Lean.Json`) ==
private def natJson (n : Nat) : Json := .num { mantissa := ↑n, exponent := 0 }

def Phase.toJson (p : Phase) : Json :=
  if p.den == 1 then .str (toString p.num)
  else .str s!"{p.num}/{p.den}"

def Node.toJson (n : Node) (idx : Nat) : Json :=
  match n with
  | .spider c p =>
    let color := match c with | .Z => "Z" | .X => "X"
    .mkObj [("id", natJson idx), ("type", .str "spider"),
            ("color", .str color), ("phase", p.toJson)]
  | .hadamard =>
    -- Default phase for Hadamard box is pi
    let phase: Phase := ⟨1, 1⟩
    .mkObj [("id", natJson idx), ("type", .str "hadamard"),
            ("phase", phase.toJson)]
  | .input id =>
    .mkObj [("id", natJson idx), ("type", .str "input"), ("ioId", natJson id)]
  | .output id =>
    .mkObj [("id", natJson idx), ("type", .str "output"), ("ioId", natJson id)]

def Edge.toJson (e : Edge) : Json :=
  .mkObj [("src", natJson e.src), ("tgt", natJson e.tgt)]

def ZXDiagram.toJson (d : ZXDiagram) : Json :=
  let nodes := d.nodes.foldl (init := (Array.empty, 0)) fun (acc, idx) opt =>
    match opt with
    | some n => (acc.push (n.toJson idx), idx + 1)
    | none   => (acc, idx + 1)
  let nodes := nodes.1
  let edges := d.edges.map Edge.toJson
  .mkObj [("nodes", .arr nodes), ("edges", .arr edges)]

-- == ProofWidgets4 widget definition ==
-- Props passed to widget
structure ZXWidgetProps where
  diagram : Json      -- JSON representation of ZXDiagram
  serverUrl : String  -- URL for Python daemon
  deriving RpcEncodable

-- Widget definition
@[widget_module]
def ZXWidget : Component ZXWidgetProps where
  javascript := include_str ".." / ".lake" / "build" / "js" / "zxDiagram.js"

-- Helper function which converts a ZXDiagram to HTML (passing the daemon URL)
def ZXDiagram.toHtml (d : ZXDiagram) : Html :=
  Html.ofComponent ZXWidget ⟨d.toJson, daemonUrl⟩ #[]
